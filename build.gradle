import org.apache.tools.ant.filters.*

apply plugin: 'groovy'

defaultTasks 'clean', 'prepareScripts', 'assembleArchive', 'test'

loadConfiguration()

repositories {
    mavenCentral()
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '2.0.5'
    groovy group: 'org.codehaus.groovy', name: 'groovy-templates', version: '2.0.5'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.0.11'
    testCompile group: 'info.cukes', name: 'cucumber-groovy', version: '1.0.11'
    testCompile group: 'org.vert-x', name: 'vertx-core', version: '1.2.3.final'
    testCompile group: 'org.vert-x', name: 'vertx-lang-groovy', version: '1.2.3.final'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

def loadConfiguration() {
    def environment = hasProperty('env') ? env : 'dev'
    ext.environment = environment
    println "Environment is set to: $environment"
    
    def configFile = file('config.groovy')
    def config = new ConfigSlurper(environment).parse(configFile.toURL())
    ext.config = config
}

task prepareScripts(type: Copy) {
	from "srv/scripts"
	into 'build/scripts'
	include '**/*'
	filter(ReplaceTokens, tokens: [GVM_VERSION:config.gvmVersion, GVM_SERVICE:config.gvmService])
}

task assembleArchive(type: Zip) {
	classifier = 'scripts'
	from "build/scripts"
	include "gvm*"
}

